import QtQuick
import QtQuick.Controls
import QtQml
import ".."
import "../filepanel"

Item {
    id: root
    anchors.fill: parent

    property var commandPaletteCommands: []
    property var appRoot: null
    property var conflictDialog: null
    property var helpDialog: null
    property var settingsDialog: null
    property var textColorOverridesOverlay: null
    property var pluginManagerDialog: null
    property var themeEditorDialog: null
    property var propertiesDialog: null
    property var providerPropertiesOverlay: null
    property var deleteConfirmDialog: null
    property var isoMountDialog: null
    property var nestedArchiveDialog: null
    property var archivePasswordDialog: null
    property var diskUsageDialog: null
    property var fileSearchDialog: null
    property var folderCompareDialog: null
    property var batchRenameDialog: null
    property var checksumDialog: null
    property var debugInformationDialog: null
    property var commandPalette: null
    property var pluginActionResultDialog: null
    property var pluginUiDialog: null
    property var steamProtonLaunchDialog: null
    property var openWithDialog: null
    property bool searchReturnAvailable: false
    property bool diskUsageReturnAvailable: false

    function isOpen(item) {
        return !!item && (item.opened || item.visible)
    }

    function ensureConflictDialog() {
        if (!root.conflictDialog) root.conflictDialog = conflictDialogComponent.createObject(root)
        return root.conflictDialog
    }

    function ensureHelpDialog() {
        if (!root.helpDialog) root.helpDialog = helpDialogComponent.createObject(root)
        return root.helpDialog
    }

    function ensureSettingsDialog() {
        if (!root.settingsDialog) root.settingsDialog = settingsDialogComponent.createObject(root)
        return root.settingsDialog
    }

    function ensureTextColorOverridesOverlay() {
        if (!root.textColorOverridesOverlay) root.textColorOverridesOverlay = textColorOverridesOverlayComponent.createObject(root)
        return root.textColorOverridesOverlay
    }

    function ensurePluginManagerDialog() {
        if (!root.pluginManagerDialog) root.pluginManagerDialog = pluginManagerDialogComponent.createObject(root)
        return root.pluginManagerDialog
    }

    function ensureThemeEditorDialog() {
        if (!root.themeEditorDialog) root.themeEditorDialog = themeEditorDialogComponent.createObject(root)
        return root.themeEditorDialog
    }

    function ensurePropertiesDialog() {
        if (!root.propertiesDialog) root.propertiesDialog = propertiesDialogComponent.createObject(root)
        return root.propertiesDialog
    }

    function ensureProviderPropertiesOverlay() {
        if (!root.providerPropertiesOverlay) root.providerPropertiesOverlay = providerPropertiesOverlayComponent.createObject(root)
        return root.providerPropertiesOverlay
    }

    function ensureDeleteConfirmDialog() {
        if (!root.deleteConfirmDialog) root.deleteConfirmDialog = deleteConfirmDialogComponent.createObject(root)
        return root.deleteConfirmDialog
    }

    function ensureIsoMountDialog() {
        if (!root.isoMountDialog) root.isoMountDialog = isoMountDialogComponent.createObject(root)
        return root.isoMountDialog
    }

    function ensureNestedArchiveDialog() {
        if (!root.nestedArchiveDialog) root.nestedArchiveDialog = nestedArchiveDialogComponent.createObject(root)
        return root.nestedArchiveDialog
    }

    function ensureArchivePasswordDialog() {
        if (!root.archivePasswordDialog) root.archivePasswordDialog = archivePasswordDialogComponent.createObject(root)
        return root.archivePasswordDialog
    }

    function ensureDiskUsageDialog() {
        if (!root.diskUsageDialog) root.diskUsageDialog = diskUsageDialogComponent.createObject(root)
        return root.diskUsageDialog
    }

    function ensureFileSearchDialog() {
        if (!root.fileSearchDialog) root.fileSearchDialog = fileSearchDialogComponent.createObject(root)
        return root.fileSearchDialog
    }
    function ensureFolderCompareDialog() {
        if (!root.folderCompareDialog) root.folderCompareDialog = folderCompareDialogComponent.createObject(root)
        return root.folderCompareDialog
    }

    function ensureBatchRenameDialog() {
        if (!root.batchRenameDialog) root.batchRenameDialog = batchRenameDialogComponent.createObject(root)
        return root.batchRenameDialog
    }

    function ensureChecksumDialog() {
        if (!root.checksumDialog) root.checksumDialog = checksumDialogComponent.createObject(root)
        return root.checksumDialog
    }

    function ensureDebugInformationDialog() {
        if (!root.debugInformationDialog) root.debugInformationDialog = debugInformationDialogComponent.createObject(root)
        return root.debugInformationDialog
    }

    function ensureSteamProtonLaunchDialog() {
        if (!root.steamProtonLaunchDialog) root.steamProtonLaunchDialog = steamProtonLaunchDialogComponent.createObject(root)
        return root.steamProtonLaunchDialog
    }

    function ensureOpenWithDialog() {
        if (!root.openWithDialog) root.openWithDialog = openWithDialogComponent.createObject(root)
        return root.openWithDialog
    }

    function ensureCommandPalette() {
        if (!root.commandPalette) root.commandPalette = commandPaletteComponent.createObject(root)
        return root.commandPalette
    }

    readonly property bool workspaceOverlayOpen: root.isOpen(root.conflictDialog)
                                                 || root.isOpen(root.helpDialog)
                                                 || root.isOpen(root.settingsDialog)
                                                 || root.isOpen(root.textColorOverridesOverlay)
                                                 || root.isOpen(root.pluginManagerDialog)
                                                 || root.isOpen(root.themeEditorDialog)
                                                 || root.isOpen(root.propertiesDialog)
                                                 || root.isOpen(root.providerPropertiesOverlay)
                                                 || root.isOpen(root.isoMountDialog)
                                                 || root.isOpen(root.nestedArchiveDialog)
                                                 || root.isOpen(root.archivePasswordDialog)
                                                 || root.isOpen(root.deleteConfirmDialog)
                                                 || root.isOpen(root.diskUsageDialog)
                                                 || root.isOpen(root.fileSearchDialog)
                                                 || root.isOpen(root.folderCompareDialog)
                                                 || root.isOpen(root.batchRenameDialog)
                                                 || root.isOpen(root.checksumDialog)
                                                 || root.isOpen(root.debugInformationDialog)
                                                 || root.isOpen(root.steamProtonLaunchDialog)
                                                 || root.isOpen(root.openWithDialog)
                                                 || root.isOpen(root.pluginActionResultDialog)
                                                 || root.isOpen(root.pluginUiDialog)
    readonly property bool anyOverlayOpen: root.workspaceOverlayOpen
                                           || root.isOpen(root.commandPalette)

    function openDeleteConfirm(paths, label, items, administrator) {
        const list = paths ? Array.from(paths) : []
        if (list.length === 0) {
            return
        }
        const details = workspaceController && workspaceController.deleteRequestDetails
                      ? workspaceController.deleteRequestDetails(list, label || "")
                      : ({})
        root.ensureDeleteConfirmDialog().openFor(list, label || "", details, items || [], administrator === true)
    }

    function openCommandPalette() {
        root.ensureCommandPalette().openPalette()
    }

    function openCommandPaletteForCommand(commandId) {
        root.ensureCommandPalette().openCommandArgument(commandId)
    }

    function openHelpDialog() {
        root.ensureHelpDialog().open()
    }

    function explicitScheme(path) {
        const value = String(path || "").trim()
        const index = value.indexOf("://")
        if (index <= 0) return ""
        const scheme = value.substring(0, index).toLowerCase()
        if (scheme.length === 0 || !/[a-z]/.test(scheme.charAt(0))) return ""
        for (let i = 0; i < scheme.length; ++i) {
            const ch = scheme.charAt(i)
            if (!/[a-z0-9+.-]/.test(ch)) return ""
        }
        return scheme
    }

    function isProviderPath(path) {
        const scheme = explicitScheme(path)
        return scheme.length > 0
            && scheme !== "file"
            && scheme !== "archive"
            && scheme !== "devices"
            && scheme !== "favorites"
    }

    function openPropertiesForPaths(paths) {
        const list = paths ? Array.from(paths) : []
        if (list.length === 0) return

        let providerCount = 0
        for (let i = 0; i < list.length; ++i) {
            if (root.isProviderPath(list[i])) {
                ++providerCount
            }
        }

        if (providerCount > 0) {
            if (providerCount !== list.length) {
                if (root.appRoot) root.appRoot.showTransientInfo("Mixed local and provider properties are not supported yet")
                return
            }
            if (list.length > 1) {
                if (root.appRoot) root.appRoot.showTransientInfo("Provider properties support one selected item for now")
                return
            }
            root.ensureProviderPropertiesOverlay()
            providerPropertiesController.load(list[0])
            providerPropertiesController.visible = true
            return
        }

        const dialog = root.ensurePropertiesDialog()
        dialog.suppressDialog = false
        dialog.accessOwnershipAdminEditMode = false
        propertiesController.loadMultiple(list)
    }

    function openAccessOwnershipAsAdministrator(path) {
        if (!path || path.length === 0) return
        const dialog = root.ensurePropertiesDialog()
        dialog.suppressDialog = false
        dialog.accessOwnershipAdminEditMode = true
        dialog.requestedTab = 3
        propertiesController.load(path)
    }

    function openSettingsDialog() {
        root.ensureSettingsDialog().open()
    }

    function openTextColorOverridesOverlay() {
        root.ensureTextColorOverridesOverlay().open()
    }

    function openPluginManagerDialog() {
        root.ensurePluginManagerDialog().open()
    }

    function openThemeEditorDialog() {
        root.ensureThemeEditorDialog().open()
    }

    function openDebugInformationDialog() {
        root.ensureDebugInformationDialog().open()
    }

    function copyPropertiesToClipboard() {
        if (root.propertiesDialog && root.propertiesDialog.visible) {
            root.propertiesDialog.copyAll()
        } else {
            const ctrl = appRoot ? appRoot.activePanelController() : null
            if (!ctrl) return
            const selected = ctrl.selectedPaths()
            if (!selected || selected.length === 0) return

            const dialog = root.ensurePropertiesDialog()
            dialog.suppressDialog = true
            if (selected.length > 1) {
                propertiesController.loadMultiple(selected)
            } else {
                propertiesController.load(selected[0])
            }
            if (workspaceController) {
                workspaceController.copyTextToClipboard(propertiesController.exportableText())
                if (appRoot) {
                    appRoot.showTransientInfo("Properties copied to clipboard")
                }
            }
            propertiesController.visible = false
            dialog.suppressDialog = false
        }
    }

    function exportPropertiesToFile(format) {
        if (root.propertiesDialog && root.propertiesDialog.visible) {
            root.propertiesDialog.openExportMenu()
        } else {
            const ctrl = appRoot ? appRoot.activePanelController() : null
            if (!ctrl) return
            const selected = ctrl.selectedPaths()
            if (!selected || selected.length === 0) return

            const dialog = root.ensurePropertiesDialog()
            dialog.suppressDialog = true
            dialog.exportDialogPending = true
            if (selected.length > 1) {
                propertiesController.loadMultiple(selected)
            } else {
                propertiesController.load(selected[0])
            }
            dialog.silentExport(format || "json")
        }
    }

    function closeTopOverlay() {
        if (root.isOpen(root.themeEditorDialog) && !root.themeEditorDialog.childDialogOpen()) {
            root.themeEditorDialog.closeEditor()
            return true
        }
        if (root.isOpen(root.pluginManagerDialog)) {
            root.pluginManagerDialog.accept()
            return true
        }
        if (root.isOpen(root.settingsDialog)) {
            root.settingsDialog.accept()
            return true
        }
        if (root.isOpen(root.textColorOverridesOverlay)) {
            root.textColorOverridesOverlay.close()
            return true
        }
        if (root.isOpen(root.helpDialog)) {
            root.helpDialog.close()
            return true
        }
        if (root.isOpen(root.propertiesDialog)) {
            root.propertiesDialog.close()
            return true
        }
        if (root.isOpen(root.providerPropertiesOverlay)) {
            root.providerPropertiesOverlay.close()
            return true
        }
        if (root.isOpen(root.isoMountDialog)) {
            root.isoMountDialog.close()
            return true
        }
        if (root.isOpen(root.nestedArchiveDialog)) {
            root.nestedArchiveDialog.close()
            return true
        }
        if (root.isOpen(root.archivePasswordDialog)) {
            root.archivePasswordDialog.close()
            return true
        }
        if (root.isOpen(root.deleteConfirmDialog)) {
            root.deleteConfirmDialog.close()
            return true
        }
        if (root.isOpen(root.diskUsageDialog)) {
            root.diskUsageDialog.accept()
            return true
        }
        if (root.isOpen(root.fileSearchDialog)) {
            root.fileSearchDialog.accept()
            return true
        }
        if (root.isOpen(root.batchRenameDialog)) {
            root.batchRenameDialog.reject()
            return true
        }
        if (root.isOpen(root.checksumDialog)) {
            root.checksumDialog.accept()
            return true
        }
        if (root.isOpen(root.debugInformationDialog)) {
            root.debugInformationDialog.close()
            return true
        }
        if (root.isOpen(root.pluginActionResultDialog)) {
            root.pluginActionResultDialog.close()
            return true
        }
        if (root.isOpen(root.pluginUiDialog)) {
            if (root.pluginUiDialog.pluginBusy) {
                return true
            }
            root.pluginUiDialog.close()
            return true
        }
        return false
    }

    function openSettingsImportDialog() {
        root.ensureSettingsDialog().openImportDialog()
    }

    function openSettingsExportDialog() {
        root.ensureSettingsDialog().openExportDialog()
    }

    function openDiskUsage(path) {
        if (!path || path.length === 0) return
        root.diskUsageReturnAvailable = false
        root.ensureDiskUsageDialog().openFor(path)
    }

    function openFileSearch(path, includeHidden) {
        if (!path || path.length === 0) return
        root.searchReturnAvailable = false
        root.ensureFileSearchDialog().openFor(path, includeHidden === true)
    }
    function openFolderCompare(leftPath, rightPath) {
        if (leftPath && rightPath) root.ensureFolderCompareDialog().openFor(leftPath, rightPath)
    }

    function openNestedArchive(controller, path, displayName, sizeText) {
        root.ensureNestedArchiveDialog().openFor(controller, path, displayName || "", sizeText || "")
    }

    function openArchivePassword(controller, path, displayName, message) {
        root.ensureArchivePasswordDialog().openFor(controller, path, displayName || "", message || "")
    }

    function openSteamProtonLaunch(controller, path) {
        if (!controller || !path || path.length === 0) return
        const options = controller.steamProtonLaunchOptionsForPath
                ? controller.steamProtonLaunchOptionsForPath(path)
                : ({})
        if (options.available === true) {
            root.ensureSteamProtonLaunchDialog().openFor(controller, path)
        } else if (controller.openPathWithSteamProton) {
            controller.openPathWithSteamProton(path)
        }
    }

    function openOpenWith(controller, paths) {
        if (controller && paths && paths.length > 0) root.ensureOpenWithDialog().openFor(controller, paths)
    }

    function reopenFileSearchResults() {
        if (!root.searchReturnAvailable) {
            return
        }
        root.ensureFileSearchDialog().reopenResults()
    }

    function reopenDiskUsageResults() {
        if (!root.diskUsageReturnAvailable) {
            return
        }
        root.ensureDiskUsageDialog().reopenResults()
    }

    function showBatchRename(paths) {
        if (!paths || paths.length === 0) return
        if (root.appRoot && root.appRoot.beginRenamePreviewSuppression) {
            root.appRoot.beginRenamePreviewSuppression(paths)
        }
        const dialog = root.ensureBatchRenameDialog()
        dialog.sourcePaths = paths
        dialog.controller = workspaceController.activePanel === 0
                                       ? workspaceController.leftPanel
                                       : workspaceController.rightPanel
        dialog.open()
    }

    function showChecksums(paths) {
        if (!paths || paths.length === 0) return
        const dialog = root.ensureChecksumDialog()
        dialog.path1 = paths[0]
        dialog.path2 = paths.length > 1 ? paths[1] : ""
        dialog.controller = workspaceController.activePanel === 0
                                     ? workspaceController.leftPanel
                                     : workspaceController.rightPanel
        dialog.open()
    }

    function openPluginActionResult(result) {
        if (result && String(result.resultType || "") === "pluginUi") {
            if (!root.pluginUiDialog) {
                root.pluginUiDialog = pluginUiDialogComponent.createObject(root)
            }
            root.pluginUiDialog.showPluginUi(result)
            return
        }
        if (!root.pluginActionResultDialog) {
            root.pluginActionResultDialog = pluginActionResultDialogComponent.createObject(root)
        }
        root.pluginActionResultDialog.showResult(result)
    }

    Component {
        id: conflictDialogComponent
        ConflictDialog {}
    }

    Component {
        id: helpDialogComponent
        HelpDialog {}
    }

    Component {
        id: settingsDialogComponent
        SettingsDialog {
            appRoot: root.appRoot
            onThemeEditorRequested: root.openThemeEditorDialog()
            onPluginManagerRequested: root.openPluginManagerDialog()
        }
    }

    Component {
        id: textColorOverridesOverlayComponent
        TextColorOverridesOverlay {
            appRoot: root.appRoot
        }
    }

    Component {
        id: pluginManagerDialogComponent
        PluginManagerDialog {}
    }

    Component {
        id: pluginUiDialogComponent
        PluginUiDialog {
            appRoot: root.appRoot
        }
    }

    Component {
        id: themeEditorDialogComponent
        ThemeEditorDialog {
            parent: Overlay.overlay
        }
    }

    Shortcut {
        sequence: "Esc"
        context: Qt.ApplicationShortcut
        enabled: root.isOpen(root.themeEditorDialog)
                 && !root.themeEditorDialog.childDialogOpen()
        onActivated: root.closeTopOverlay()
        onActivatedAmbiguously: root.closeTopOverlay()
    }

    Component {
        id: propertiesDialogComponent
        PropertiesDialog {
            appRoot: root.appRoot
        }
    }

    Component {
        id: providerPropertiesOverlayComponent
        ProviderPropertiesOverlay {}
    }

    Component {
        id: deleteConfirmDialogComponent
        DeleteConfirmDialog {
            appRoot: root.appRoot
        }
    }

    Component {
        id: isoMountDialogComponent
        IsoMountDialog {}
    }

    Component {
        id: nestedArchiveDialogComponent
        NestedArchiveDialog {}
    }

    Component {
        id: archivePasswordDialogComponent
        ArchivePasswordDialog {}
    }

    Component {
        id: diskUsageDialogComponent
        DiskUsageDialog {
            appRoot: root.appRoot
            onResultOpened: {
                root.diskUsageReturnAvailable = true
            }
            onResultsReset: {
                root.diskUsageReturnAvailable = false
            }
        }
    }

    Component {
        id: fileSearchDialogComponent
        FileSearchDialog {
            appRoot: root.appRoot
            onResultOpened: {
                root.searchReturnAvailable = true
            }
            onSearchContextReset: {
                root.searchReturnAvailable = false
            }
        }
    }
    Component {
        id: folderCompareDialogComponent
        FolderCompareDialog { appRoot: root.appRoot }
    }

    Component {
        id: batchRenameDialogComponent
        BatchRenameDialog {
            appRoot: root.appRoot
        }
    }

    Component {
        id: checksumDialogComponent
        ChecksumDialog {}
    }

    Component {
        id: debugInformationDialogComponent
        DebugInformationDialog {
            appRoot: root.appRoot
        }
    }

    Component {
        id: steamProtonLaunchDialogComponent
        SteamProtonLaunchDialog {}
    }

    Component {
        id: openWithDialogComponent
        OpenWithDialog {
            onSteamProtonRequested: (targetController, path) => root.openSteamProtonLaunch(targetController, path)
        }
    }

    Component {
        id: commandPaletteComponent
        CommandPalette {
            commands: root.commandPaletteCommands
            activePanelController: root.appRoot ? root.appRoot.activePanelController : null
        }
    }

    Component {
        id: pluginActionResultDialogComponent
        PluginActionResultDialog {}
    }

    Connections {
        target: workspaceController.operationQueue
        function onConflictDetected(source, destination, sourceSize, sourceModified, destSize, destModified) {
            const dialog = root.ensureConflictDialog()
            dialog.sourcePath = source
            dialog.destinationPath = destination
            dialog.sourceSize = sourceSize
            dialog.sourceModified = sourceModified
            dialog.destSize = destSize
            dialog.destModified = destModified
            dialog.open()
        }
    }

    Connections {
        target: propertiesController
        function onVisibleChanged() {
            if (propertiesController.visible) {
                root.ensurePropertiesDialog()
            }
        }
    }

    Connections {
        target: providerPropertiesController
        function onVisibleChanged() {
            if (providerPropertiesController.visible) {
                root.ensureProviderPropertiesOverlay()
            }
        }
    }

    Connections {
        target: workspaceController
        function onDeleteRequested(paths, label, items) {
            root.openDeleteConfirm(paths, label, items, false)
        }
        function onDeleteAsAdministratorRequested(paths, label, items) {
            root.openDeleteConfirm(paths, label, items, true)
        }
        function onMountIsoRequested(path) {
            root.ensureIsoMountDialog().openFor(path)
        }
        function onArchivePasswordRequested(path, displayName, message) {
            root.openArchivePassword(workspaceController, path, displayName, message)
        }
    }

    Connections {
        target: workspaceController.leftPanel
        function onNestedArchiveOpenRequested(path, displayName, sizeText) {
            root.openNestedArchive(workspaceController.leftPanel, path, displayName, sizeText)
        }

        function onArchivePasswordRequested(path, displayName, message) {
            root.openArchivePassword(workspaceController.leftPanel, path, displayName, message)
        }

        function onRevealProperties(paths) {
            root.openPropertiesForPaths(paths)
        }
        function onRevealAccessOwnershipAsAdministrator(path) {
            root.openAccessOwnershipAsAdministrator(path)
        }
    }

    Connections {
        target: workspaceController.rightPanel
        function onNestedArchiveOpenRequested(path, displayName, sizeText) {
            root.openNestedArchive(workspaceController.rightPanel, path, displayName, sizeText)
        }

        function onArchivePasswordRequested(path, displayName, message) {
            root.openArchivePassword(workspaceController.rightPanel, path, displayName, message)
        }

        function onRevealProperties(paths) {
            root.openPropertiesForPaths(paths)
        }
        function onRevealAccessOwnershipAsAdministrator(path) {
            root.openAccessOwnershipAsAdministrator(path)
        }
    }
}
