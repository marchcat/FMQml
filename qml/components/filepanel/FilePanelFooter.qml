import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import "../../style"
import "../common"

AmbientPanelBackground {
    id: root

    property var controller
    property var placesModel
    property bool active: false
    property bool deviceRootMode: false
    property bool favoritesRootMode: false
    property int favoritesPinnedCount: 0
    property int favoritesFrequentCount: 0
    property int favoritesTagCount: 0
    property int viewMode: 0
    property string currentPath: ""
    property bool showLoadingRail: false
    property string statusMessage: ""
    property bool isCurrentPathArchive: false
    property bool isCurrentPathManagedIsoMount: false
    property real loadingProgress: -1
    property string loadingProgressText: ""
    property bool loadingCancelable: false
    readonly property color panelAccent: Theme.accent
    property int gridIconSize: 48
    property int gridIconMinSize: 32
    property int gridIconMaxSize: 96
    property int briefRowHeight: 28
    property int briefRowMinHeight: 22
    property int briefRowMaxHeight: 64
    property var loadingFolderNameProvider
    property int storageRevision: 0
    property string deviceRootPrimaryStatus: ""
    property string deviceRootSecondaryStatus: ""
    property string deviceRootStorageText: ""
    property string deviceRootStorageTooltip: ""
    property real deviceRootUsagePercent: 0
    property bool deviceRootStorageCritical: false

    signal gridIconSizeRequested(int value)
    signal briefRowHeightRequested(int value)
    signal cancelLoadingRequested()

    Connections {
        target: root.placesModel
        ignoreUnknownSignals: true
        function onDataChanged() { root.storageRevision += 1 }
        function onModelReset() { root.storageRevision += 1 }
        function onRowsInserted() { root.storageRevision += 1 }
        function onRowsRemoved() { root.storageRevision += 1 }
    }

    readonly property int pathRole: Qt.UserRole + 2
    readonly property int isDriveRole: Qt.UserRole + 4
    readonly property int totalSpaceRole: Qt.UserRole + 5
    readonly property int freeSpaceRole: Qt.UserRole + 6
    readonly property int usagePercentRole: Qt.UserRole + 8
    readonly property int isReadyRole: Qt.UserRole + 11
    readonly property int isCriticalRole: Qt.UserRole + 12
    readonly property int driveIndex: {
        storageRevision
        findDriveIndex()
    }
    readonly property bool hasDrive: driveIndex >= 0
    readonly property real totalSpace: {
        storageRevision
        hasDrive ? Number(placesModel.data(placesModel.index(driveIndex, 0), totalSpaceRole)) : 0
    }
    readonly property real freeSpace: {
        storageRevision
        hasDrive ? Number(placesModel.data(placesModel.index(driveIndex, 0), freeSpaceRole)) : 0
    }
    readonly property real usagePercent: {
        storageRevision
        hasDrive ? Number(placesModel.data(placesModel.index(driveIndex, 0), usagePercentRole)) : 0
    }
    readonly property bool driveReady: {
        storageRevision
        hasDrive ? !!placesModel.data(placesModel.index(driveIndex, 0), isReadyRole) : false
    }
    readonly property bool driveCritical: {
        storageRevision
        hasDrive ? !!placesModel.data(placesModel.index(driveIndex, 0), isCriticalRole) : false
    }
    readonly property int providerStorageRevision: controller ? controller.storageInfoRevision : 0
    readonly property bool currentPathIsProvider: {
        const value = String(currentPath || "").trim()
        const schemeEnd = value.indexOf("://")
        if (schemeEnd <= 0) return false
        const scheme = value.substring(0, schemeEnd).toLowerCase()
        return scheme !== "file" && scheme !== "archive" && scheme !== "devices" && scheme !== "favorites"
    }
    readonly property var providerStorageInfo: {
        providerStorageRevision
        if (!currentPathIsProvider || !controller || !controller.storageInfoForPath) {
            return {}
        }
        return controller.storageInfoForPath(currentPath)
    }
    readonly property bool hasProviderStorage: currentPathIsProvider && providerStorageInfo && providerStorageInfo.valid === true
    readonly property real providerTotalSpace: hasProviderStorage ? Number(providerStorageInfo.total) : 0
    readonly property real providerUsedSpace: hasProviderStorage ? Number(providerStorageInfo.used) : 0
    readonly property real providerFreeSpace: hasProviderStorage ? Number(providerStorageInfo.free) : 0
    readonly property real providerUsagePercent: providerTotalSpace > 0
                                                   ? Math.max(0, Math.min(1, providerUsedSpace / providerTotalSpace))
                                                   : 0
    readonly property bool providerStorageCritical: hasProviderStorage
                                                    && providerTotalSpace > 0
                                                    && providerFreeSpace >= 0
                                                    && providerFreeSpace / providerTotalSpace < 0.10
    readonly property bool zoomVisible: viewMode === 1 || viewMode === 2
    readonly property bool hasLoadingProgress: showLoadingRail && loadingProgress >= 0
    readonly property int zoomValue: viewMode === 1 ? gridIconSize : briefRowHeight
    readonly property int zoomMin: viewMode === 1 ? gridIconMinSize : briefRowMinHeight
    readonly property int zoomMax: viewMode === 1 ? gridIconMaxSize : briefRowMaxHeight
    readonly property int zoomStep: viewMode === 1 ? 4 : 2

    implicitHeight: Math.max(34, Theme.controlHeight - 2)
    baseColor: Theme.panelSurfaceStrong
    strength: 0.28
    cornerRadius: Theme.innerRadius(Theme.panelRadius, 1)
    topLeftCornerRadius: 0
    topRightCornerRadius: 0
    border.width: 0

    Rectangle {
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.top: parent.top
        height: 1
        color: root.active
               ? Theme.activePanelStrokeSoft
               : Theme.panelStrokeSubtle
    }

    function normalizePath(path) {
        let value = String(path || "").replace(/\\/g, "/")
        if (value.length >= 2 && value.charAt(1) === ":") {
            value = value.charAt(0).toUpperCase() + value.slice(1)
        }
        if (value.length === 2 && value.charAt(1) === ":") {
            value += "/"
        }
        return value
    }

    function driveRoot(path) {
        const value = normalizePath(path)
        if (value.length >= 3 && value.charAt(1) === ":" && value.charAt(2) === "/") {
            return value.slice(0, 3)
        }
        return value
    }

    function findDriveIndex() {
        if (!placesModel || !currentPath || String(currentPath).indexOf("archive://") === 0) {
            return -1
        }

        const current = normalizePath(currentPath)
        const rootPath = driveRoot(current)
        const rows = placesModel.rowCount()
        let bestIndex = -1
        let bestLength = 0

        for (let i = 0; i < rows; ++i) {
            const idx = placesModel.index(i, 0)
            if (!placesModel.data(idx, isDriveRole)) {
                continue
            }

            const drivePath = driveRoot(placesModel.data(idx, pathRole))
            if (drivePath.length === 0) {
                continue
            }

            if ((current === drivePath || current.indexOf(drivePath) === 0 || rootPath === drivePath)
                    && drivePath.length > bestLength) {
                bestIndex = i
                bestLength = drivePath.length
            }
        }

        return bestIndex
    }

    function formatBytes(bytes) {
        const value = Number(bytes)
        if (!isFinite(value) || value <= 0) return "-"
        const tb = 1024 * 1024 * 1024 * 1024
        const gb = 1024 * 1024 * 1024
        const mb = 1024 * 1024
        if (value >= tb) return (value / tb).toFixed(2) + " TB"
        if (value >= gb) return (value / gb).toFixed(1) + " GB"
        if (value >= mb) return Math.round(value / mb) + " MB"
        return Math.round(value / 1024) + " KB"
    }

    function statusText() {
        if (favoritesRootMode) {
            return "Favorites"
        }
        if (deviceRootMode) {
            return deviceRootPrimaryStatus
        }
        if (showLoadingRail) {
            if (root.loadingProgressText.length > 0) {
                return root.loadingProgressText
            }
            return isCurrentPathArchive ? "Loading archive..." : "Scanning folder"
        }
        if (statusMessage.length > 0) {
            return statusMessage
        }
        if (!controller || !controller.directoryModel) {
            return ""
        }

        const selected = controller.directoryModel.selectedCount
        const count = controller.directoryModel.count
        if (selected > 0) {
            return selected + " selected of " + count
        }
        return count + (count === 1 ? " item" : " items")
    }

    function secondaryStatusText() {
        if (favoritesRootMode) {
            return "Pinned " + root.favoritesPinnedCount
                    + " - Frequent " + root.favoritesFrequentCount
                    + " - Tags " + root.favoritesTagCount
        }
        if (deviceRootMode) {
            return deviceRootSecondaryStatus
        }
        if (!showLoadingRail) {
            return ""
        }
        if (loadingFolderNameProvider) {
            return "Reading items from " + loadingFolderNameProvider()
        }
        return "Reading items"
    }

    function storageText() {
        if (favoritesRootMode) {
            return ""
        }
        if (isCurrentPathManagedIsoMount) {
            return ""
        }
        if (deviceRootMode) {
            return deviceRootStorageText
        }
        if (root.hasProviderStorage) {
            if (root.providerFreeSpace >= 0) {
                return root.formatBytes(root.providerFreeSpace) + " available"
            }
            if (root.providerUsedSpace > 0) {
                return root.formatBytes(root.providerUsedSpace) + " used"
            }
        }
        if (root.hasDrive && root.driveReady && root.totalSpace > 0) {
            return root.formatBytes(root.freeSpace) + " free"
        }
        return "Size unavailable"
    }

    function storageTooltipText() {
        if (favoritesRootMode) {
            return "Storage usage is not shown for virtual Favorites"
        }
        if (isCurrentPathManagedIsoMount) {
            return "Storage usage is not shown for a read-only ISO image"
        }
        if (deviceRootMode) {
            return deviceRootStorageTooltip
        }
        if (root.hasProviderStorage) {
            const used = root.providerUsedSpace >= 0 ? root.formatBytes(root.providerUsedSpace) : "-"
            const available = root.providerFreeSpace >= 0 ? root.formatBytes(root.providerFreeSpace) : "-"
            const total = root.providerTotalSpace > 0 ? root.formatBytes(root.providerTotalSpace) : "unlimited or unknown"
            return "Used " + used + " - Available " + available + " - Total " + total
        }
        if (root.hasDrive && root.driveReady && root.totalSpace > 0) {
            return root.formatBytes(root.freeSpace) + " free of " + root.formatBytes(root.totalSpace)
        }
        return "Storage size is unavailable for this location"
    }

        Rectangle {
            anchors.left: parent.left
            anchors.top: parent.top
            anchors.bottom: parent.bottom
            anchors.topMargin: 8
            anchors.bottomMargin: 8
            width: 2
            radius: 1
            color: root.panelAccent
            opacity: themeController.isDark ? 0.88 : 0.96
            visible: root.active
        }

    RowLayout {
        anchors.fill: parent
        anchors.leftMargin: 12
        anchors.rightMargin: 10
        spacing: 12

        BusyIndicator {
            Layout.preferredWidth: 16
            Layout.preferredHeight: 16
            running: root.showLoadingRail
            visible: running
        }

        Rectangle {
            Layout.preferredWidth: 7
            Layout.preferredHeight: 7
            radius: 4
            visible: !root.showLoadingRail && root.statusMessage.length > 0
            color: root.panelAccent
            opacity: 0.9
        }

        ColumnLayout {
            Layout.fillWidth: true
            Layout.minimumWidth: 0
            spacing: 0

            Label {
                Layout.fillWidth: true
                text: root.statusText()
                color: TextColors.statusText
                font.family: Theme.fontFamily
                font.pixelSize: Theme.fontSizeCaption
                elide: Text.ElideRight
                verticalAlignment: Text.AlignVCenter
            }

            Label {
                Layout.fillWidth: true
                visible: root.showLoadingRail || root.deviceRootMode
                         || root.favoritesRootMode
                text: root.secondaryStatusText()
                color: TextColors.statusText
                font.family: Theme.fontFamily
                font.pixelSize: Theme.fontSizeMicro
                elide: Text.ElideRight
                verticalAlignment: Text.AlignVCenter
            }
        }

        Rectangle {
            Layout.preferredWidth: 1
            Layout.fillHeight: true
            Layout.topMargin: 6
            Layout.bottomMargin: 6
            color: Theme.withAlpha(Theme.panelBorder, themeController.isDark ? 0.34 : 0.85)
            opacity: themeController.isDark ? 0.78 : 0.9
            visible: !root.favoritesRootMode && !root.isCurrentPathManagedIsoMount
        }

        RowLayout {
            Layout.preferredWidth: 188
            Layout.maximumWidth: 188
            Layout.minimumWidth: 136
            visible: !root.favoritesRootMode && !root.isCurrentPathManagedIsoMount
            spacing: 8

            Label {
                Layout.preferredWidth: 84
                text: root.storageText()
                color: (root.deviceRootMode
                        ? root.deviceRootStorageCritical
                        : (root.currentPathIsProvider ? root.providerStorageCritical : root.driveCritical))
                       ? Theme.danger : TextColors.statusText
                font.family: Theme.fontFamily
                font.pixelSize: Theme.fontSizeMicro
                elide: Text.ElideRight
            }

            Item {
                Layout.fillWidth: true
                Layout.preferredHeight: 8

                Rectangle {
                    anchors.left: parent.left
                    anchors.right: parent.right
                    anchors.verticalCenter: parent.verticalCenter
                    height: 4
                    radius: 2
                    color: Theme.withAlpha(Theme.panelBorder, themeController.isDark ? 0.34 : 0.65)
                }

                Rectangle {
                    anchors.left: parent.left
                    anchors.verticalCenter: parent.verticalCenter
                    width: root.deviceRootMode
                           ? (root.deviceRootUsagePercent > 0
                                  ? Math.max(4, Math.min(1, Math.max(0, root.deviceRootUsagePercent)) * parent.width)
                                  : 0)
                           : (root.currentPathIsProvider
                              ? (root.hasProviderStorage && root.providerTotalSpace > 0
                                 ? Math.max(4, Math.min(1, Math.max(0, root.providerUsagePercent)) * parent.width)
                                 : 0)
                           : (root.hasDrive && root.driveReady && root.totalSpace > 0
                                  ? Math.max(4, Math.min(1, Math.max(0, root.usagePercent)) * parent.width)
                                  : 0))
                    height: 4
                    radius: 2
                    color: (root.deviceRootMode
                            ? root.deviceRootStorageCritical
                            : (root.currentPathIsProvider ? root.providerStorageCritical : root.driveCritical))
                           ? Theme.danger : root.panelAccent

                    Behavior on width {
                        NumberAnimation { duration: 220; easing.type: Easing.OutCubic }
                    }
                }
            }

            ToolTip.visible: storageHover.hovered
            ToolTip.text: root.storageTooltipText()

            HoverHandler {
                id: storageHover
            }
        }

        Rectangle {
            Layout.preferredWidth: 64
            Layout.preferredHeight: 22
            radius: 4
            visible: root.showLoadingRail && root.loadingCancelable
            color: cancelMouse.pressed
                   ? Theme.withAlpha(Theme.danger, themeController.isDark ? 0.20 : 0.14)
                   : (cancelMouse.hovered
                      ? Theme.withAlpha(Theme.danger, themeController.isDark ? 0.13 : 0.08)
                      : Theme.panelSurfaceSoft)
            border.color: Theme.withAlpha(Theme.danger, cancelMouse.hovered ? 0.62 : 0.38)
            border.width: 1

            Label {
                anchors.centerIn: parent
                text: "Cancel"
                color: cancelMouse.hovered ? Theme.danger : Theme.textPrimary
                font.family: Theme.fontFamily
                font.pixelSize: Theme.fontSizeMicro
                font.weight: Font.DemiBold
            }

            MouseArea {
                id: cancelMouse
                anchors.fill: parent
                hoverEnabled: true
                cursorShape: Qt.PointingHandCursor
                onClicked: root.cancelLoadingRequested()
            }
        }

        Rectangle {
            Layout.preferredWidth: 1
            Layout.fillHeight: true
            Layout.topMargin: 6
            Layout.bottomMargin: 6
            visible: root.zoomVisible
                     && !root.favoritesRootMode
            color: Theme.withAlpha(Theme.panelBorder, themeController.isDark ? 0.34 : 0.85)
            opacity: themeController.isDark ? 0.78 : 0.9
        }

        Slider {
            id: zoomSlider
            Layout.preferredWidth: 104
            Layout.preferredHeight: 22
            visible: root.zoomVisible
                     && !root.favoritesRootMode
            from: root.zoomMin
            to: root.zoomMax
            stepSize: root.zoomStep
            snapMode: Slider.SnapAlways
            value: root.zoomValue
            focusPolicy: Qt.StrongFocus

            onMoved: {
                const snapped = Math.round(value / stepSize) * stepSize
                if (root.viewMode === 1) {
                    root.gridIconSizeRequested(snapped)
                } else if (root.viewMode === 2) {
                    root.briefRowHeightRequested(snapped)
                }
            }

            background: Item {
                anchors.fill: parent

                Rectangle {
                    anchors.left: parent.left
                    anchors.right: parent.right
                    anchors.verticalCenter: parent.verticalCenter
                    height: 4
                    radius: 2
                    color: Theme.withAlpha(Theme.panelBorder, themeController.isDark ? 0.36 : 0.62)
                }

                Rectangle {
                    anchors.left: parent.left
                    anchors.verticalCenter: parent.verticalCenter
                    width: zoomSlider.visualPosition * parent.width
                    height: 4
                    radius: 2
                    color: Theme.accent
                }
            }

            handle: Rectangle {
                x: zoomSlider.leftPadding + zoomSlider.visualPosition * (zoomSlider.availableWidth - width)
                y: zoomSlider.topPadding + zoomSlider.availableHeight / 2 - height / 2
                width: 10
                height: 10
                radius: 5
                color: zoomSlider.pressed ? Theme.accent : Theme.panelSurface
                border.color: Theme.accent
                border.width: 1
            }

            ToolTip.visible: hovered || pressed
            ToolTip.text: root.viewMode === 1
                          ? "Icon size: " + root.gridIconSize + " px"
                          : "Density: " + root.briefRowHeight + " px"
        }
    }

    Rectangle {
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.bottom: parent.bottom
        height: 3
        visible: root.hasLoadingProgress
        color: Theme.withAlpha(root.panelAccent, themeController.isDark ? 0.16 : 0.10)

        Rectangle {
            anchors.left: parent.left
            anchors.top: parent.top
            anchors.bottom: parent.bottom
            width: parent.width * Math.max(0, Math.min(1, root.loadingProgress))
            color: root.panelAccent

            Behavior on width {
                NumberAnimation { duration: 120; easing.type: Easing.OutCubic }
            }
        }
    }
}
